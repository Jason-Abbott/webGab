<!--#include file="data/webGab_data.inc"-->
<%
' Copyright 1999 Jason Abbott (jabbott@uidaho.edu)
' updated 08/12/1999

' if cancel is hit then send back to specified page

if Request.Form("cancel") = "Cancel" then
	response.redirect "webGab_" & Request.Form("return")
end if

dim strDate, strSubject, strBody, tfReply

strDate = Now
strSubject = replace(Request.Form("subject"), "'", "''")
strBody = replace(Request.Form("body"), "'", "''")

if Request.Form("reply") = "on" then
	tfReply = True
else
	tfReply = False
end if

' otherwise update the database

strQuery = "INSERT INTO gab_items (" _
	& "msg_subject, "_
	& "msg_author, " _
	& "msg_email, " _
	& "msg_body, " _
	& "msg_parent, " _
	& "msg_topic, " _
	& "msg_reply, " _
	& "msg_ip, " _
	& "msg_date" _
	& ") VALUES ('" _
	& strSubject & "', '" _
	& Request.Form("author") & "', '" _
	& Request.Form("email") & "', '" _
	& strBody & "', '" _
	& Request.Form("parent_id") & "', '" _
	& Request.Form("topic_id") & "', " _
	& tfReply & ", '" _
	& Request.ServerVariables("REMOTE_ADDR") & "', '" _
	& strDate & "')"

' adCmdText = &H0001

db.Execute strQuery,,&H0001

' get new id

strQuery = "SELECT msg_id, msg_date, msg_subject FROM gab_items " _
	& "WHERE msg_subject='" & Request.Form("subject") _
	& "' AND msg_date=#" & strDate & "#"

'response.write strQuery
	
Set rsTemp = db.Execute(strQuery,,&H0001)
intNewID = rsTemp("msg_id")
rsTemp.Close
Set rsTemp = nothing

db.Close
Set db = nothing

' if the original author requested e-mail replies then
' send the e-mail

if Request.Form("reply_to") <> "" then
	Set JMail = Server.CreateObject("JMail.SMTPMail")
	JMail.ServerAddress = "borah.engboi.uidaho.edu"
	JMail.Sender = Request.Form("email")
	JMail.SenderName = Request.Form("author")
	JMail.Subject = strSubject
	JMail.AddHeader "Originating-IP", Request.ServerVariables("REMOTE_ADDR")
	JMail.AddRecipient Request.Form("reply_to")
	JMail.Body = strBody & VbCrLf & VbCrLf _
		& "-----------------------------------------------------------" & VbCrLf _
		& "This message was generated by webGab, sent at your request." & VbCrLf _
		& "-----------------------------------------------------------" & VbCrLf
'	on error resume next
	JMail.Execute
end if

Application(dataName & Request.Form("topic_id") & "Msgs") = ""

if strBody <> "" then
	response.redirect "webGab_detail.asp?msg_id=" & intNewID & "&refresh=1"
else
	response.redirect "webGab_intro.asp?topic_id=" _
		& Request.Form("topic_id") & "&refresh=1"
end if 
%>